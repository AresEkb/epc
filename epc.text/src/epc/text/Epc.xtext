grammar epc.text.Epc with org.eclipse.xtext.common.Terminals

import "http://www.example.org/epc"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model returns Model:
    'Model' name=EString
    '{'
        ('Version' version=EString)?
        ('Description' description=EString)?
        elements+=ModelElement*
    '}';

ModelElement returns ModelElement:
    Event | Function | OrganizationUnit | Resource | System |
    Gate | ControlFlow | InformationFlow | Participation | Association;

Event returns Event:
    'Event' name=EString;

Function returns Function:
    'Function' name=EString;

System returns System:
    'System' name=EString;

OrganizationUnit returns OrganizationUnit:
    'OrganizationUnit' name=EString;

Resource returns Resource:
    kind=ResourceKind name=EString;

Gate returns Gate:
    kind=GateKind inputs+=[NamedElement|EString]* '->' outputs+=[NamedElement|EString]*;

ControlFlow returns ControlFlow:
    'ControlFlow' source=[NamedElement|EString] target=[NamedElement|EString];

InformationFlow returns InformationFlow:
    'InformationFlow' source=[NamedElement|EString] kind=InformationFlowKind? target=[NamedElement|EString];

Participation returns Participation:
    'Participation' source=[NamedElement|EString] kind=ParticipationKind? target=[NamedElement|EString];

Association returns Association:
    'Association' source=[NamedElement|EString] target=[NamedElement|EString];

enum ResourceKind returns ResourceKind:
    unknown = 'Resource' |
    information = 'Information' |
    paperDocument = 'PaperDocument' |
    electronicDocument = 'ElectronicDocument' |
    dataBase = 'DataBase';

enum GateKind returns GateKind:
    andGate = 'And' |
    orGate = 'Or' |
    xorGate = 'Xor';

enum InformationFlowKind returns InformationFlowKind:
    changes = 'changes' |
    outputs = 'outputs' |
    creates = 'creates' |
    usedBy = 'usedBy' |
    inputFor = 'inputFor';

enum ParticipationKind returns ParticipationKind:
    performs = 'performs' |
    supports = 'supports' |
    controls = 'controls';

EString returns ecore::EString:
    STRING | ID;
