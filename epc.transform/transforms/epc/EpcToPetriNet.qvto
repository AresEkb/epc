import Helpers;

modeltype EPC uses 'http://www.example.org/epc';
modeltype PN uses 'http://org.pnml.tools/epnk/pnmlcoremodel';
modeltype PT uses 'http://org.pnml.tools/epnk/types/ptnet';
modeltype PDT uses 'http://org.pnml.tools/epnk/datatypes';

transformation EpcToPetriNet(in epc : EPC, out pn : PN);

main()
{
    epc.rootObjects()[Model]->toPetriNet();
}

mapping EPC::Model::toPetriNet() : PN::PetriNetDoc
{
    net := object PN::PetriNet {
        type := object PT::PTNet { };
        id := 'model'.uniqueId();
        name := self.toName();
        page := object PN::Page {
            id := 'page'.uniqueId();
            name := self.toName();
            _object := self.elements->toNode();
            // All nodes are mapped already, so we can use non-late resolve in the following mappings
            self.elements->toObjects()->forEach (objs) {
                _object += objs;
            };
            self.elements[EPC::Gate]->toOrSync()->forEach (objs) {
                _object += objs;
            };
        };
    };
}

mapping EPC::ModelElement::toNode() : PN::Node
disjuncts
    EPC::Event::toPlace,
    EPC::Function::toTransition
{
}

mapping EPC::Event::toPlace() : PT::Place
{
    id := 'p'.uniqueId();
    name := self.toName();
    if (not self.model.elements[EPC::Connection]->exists(target = self) and
        not self.model.elements[EPC::Gate]->exists(outputs->includes(self))) {
        initialMarking := object PT::PTMarking {
            text := new PDT::NonNegativeInteger('1');
        };
    };
}

mapping EPC::Function::toTransition() : PN::Transition
{
    id := 't'.uniqueId();
    name := self.toName();
}

mapping EPC::ModelElement::toObjects() : List(PN::Object)
disjuncts
    EPC::ControlFlow::toArc,
    EPC::Gate::toAnd,
    EPC::Gate::toXor,
    EPC::Gate::toOr
{
}

mapping EPC::ControlFlow::toArc() : List(PN::Object)
{
    if (self.source.oclIsKindOf(Event) and self.target.oclIsKindOf(Event)) {
        var transition := object PN::Transition {
            id := 't'.uniqueId();
        };
        result += transition;
        result += object PN::Arc {
            id := 'a'.uniqueId();
            source := self.source.late resolveoneIn(EPC::ModelElement::toNode);
            target := transition;
        };
        result += object PN::Arc {
            id := 'a'.uniqueId();
            source := transition;
            target := self.target.late resolveoneIn(EPC::ModelElement::toNode);
        };
    }
    elif (self.source.oclIsKindOf(Function) and self.target.oclIsKindOf(Function)) {
        var place := object PN::Place {
            id := 'p'.uniqueId();
        };
        result += place;
        result += object PN::Arc {
            id := 'a'.uniqueId();
            source := self.source.late resolveoneIn(EPC::ModelElement::toNode);
            target := place;
        };
        result += object PN::Arc {
            id := 'a'.uniqueId();
            source := place;
            target := self.target.late resolveoneIn(EPC::ModelElement::toNode);
        };
    }
    else {
        result += object PN::Arc {
            id := 'a'.uniqueId();
            source := self.source.late resolveoneIn(EPC::ModelElement::toNode);
            target := self.target.late resolveoneIn(EPC::ModelElement::toNode);
        };
    }
}

mapping EPC::Gate::toAnd() : List(PN::Object)
when { self.kind = EPC::GateKind::andGate }
{
    var mainNode : PN::Transition;

    var inputIsFunction := self.inputs->size() = 1 and self.inputs->forAll(oclIsKindOf(EPC::Function));
    var outputIsFunction := self.outputs->size() = 1 and self.outputs->forAll(oclIsKindOf(EPC::Function));

    // Main node
    if (inputIsFunction) {
        mainNode := self.inputs->at(1).resolveoneIn(EPC::ModelElement::toNode).oclAsType(PN::Transition);
    }
    elif (outputIsFunction) {
        mainNode := self.outputs->at(1).resolveoneIn(EPC::ModelElement::toNode).oclAsType(PN::Transition);
    }
    else {
        mainNode := object PN::Transition {
            id := 't'.uniqueId();
            name := object PN::Name { text := 'And' };
        };
        result += mainNode;
    };

    // Input nodes
    if (not inputIsFunction) {
        self.inputs->forEach (element) {
            var sourceNode := element.resolveoneIn(EPC::ModelElement::toNode);
            var place : PN::Place;
            if (sourceNode.oclIsKindOf(PN::Place)) {
                place := sourceNode.oclAsType(PN::Place);
            }
            else {
                place := object PN::Place {
                    id := 'p'.uniqueId();
                };
                result += place;
                result += object PN::Arc {
                    id := 'a'.uniqueId();
                    source := sourceNode;
                    target := place;
                };
            };
            result += object PN::Arc {
                id := 'a'.uniqueId();
                source := place;
                target := mainNode;
            };
        };
    };

    // Output nodes
    if (inputIsFunction or not outputIsFunction) {
        self.outputs->forEach (element) {
            var targetNode := element.resolveoneIn(EPC::ModelElement::toNode);
            var place : PN::Place;
            if (targetNode.oclIsKindOf(PN::Place)) {
                place := targetNode.oclAsType(PN::Place);
            }
            else {
                place := object PN::Place {
                    id := 'p'.uniqueId();
                };
                result += place;
                result += object PN::Arc {
                    id := 'a'.uniqueId();
                    source := place;
                    target := targetNode;
                };
            };
            result += object PN::Arc {
                id := 'a'.uniqueId();
                source := mainNode;
                target := place;
            };
        };
    };
}

mapping EPC::Gate::toXor() : List(PN::Object)
when { self.kind = EPC::GateKind::xorGate }
{
    var mainNode : PN::Place;

    var inputIsEvent := self.inputs->size() = 1 and self.inputs->forAll(oclIsKindOf(EPC::Event));
    var outputIsEvent := self.outputs->size() = 1 and self.outputs->forAll(oclIsKindOf(EPC::Event));

    // Main node
    if (inputIsEvent) {
        mainNode := self.inputs->at(1).resolveoneIn(EPC::ModelElement::toNode).oclAsType(PN::Place);
    }
    elif (outputIsEvent) {
        mainNode := self.outputs->at(1).resolveoneIn(EPC::ModelElement::toNode).oclAsType(PN::Place);
    }
    else {
        mainNode := object PN::Place {
            id := 'p'.uniqueId();
            name := object PN::Name { text := 'Xor' };
        };
        result += mainNode;
    };

    // Input nodes
    if (not inputIsEvent) {
        self.inputs->forEach (element) {
            var sourceNode := element.resolveoneIn(EPC::ModelElement::toNode);
            var transition : PN::Transition;
            if (sourceNode.oclIsKindOf(PN::Transition)) {
                transition := sourceNode.oclAsType(PN::Transition);
            }
            else {
                transition := object PN::Transition {
                    id := 't'.uniqueId();
                };
                result += transition;
                result += object PN::Arc {
                    id := 'a'.uniqueId();
                    source := sourceNode;
                    target := transition;
                };
            };
            result += object PN::Arc {
                id := 'a'.uniqueId();
                source := transition;
                target := mainNode;
            };
        };
    };

    // Output nodes
    if (inputIsEvent or not outputIsEvent) {
        self.outputs->forEach (element) {
            var targetNode := element.resolveoneIn(EPC::ModelElement::toNode);
            var transition : PN::Transition;
            if (targetNode.oclIsKindOf(PN::Transition)) {
                transition := targetNode.oclAsType(PN::Transition);
            }
            else {
                transition := object PN::Transition {
                    id := 't'.uniqueId();
                };
                result += transition;
                result += object PN::Arc {
                    id := 'a'.uniqueId();
                    source := transition;
                    target := targetNode;
                };
            };
            result += object PN::Arc {
                id := 'a'.uniqueId();
                source := mainNode;
                target := transition;
            };
        };
    };
}

mapping EPC::Gate::toOr() : List(PN::Object)
when { self.kind = EPC::GateKind::orGate }
{
    var mainNode : PN::Place;

    var inputIsEvent := self.inputs->size() = 1 and self.inputs->forAll(oclIsKindOf(EPC::Event));
    var outputIsEvent := self.outputs->size() = 1 and self.outputs->forAll(oclIsKindOf(EPC::Event));

    // Main node
    if (inputIsEvent) {
        mainNode := self.inputs->at(1).resolveoneIn(EPC::ModelElement::toNode).oclAsType(PN::Place);
    }
    elif (outputIsEvent) {
        mainNode := self.outputs->at(1).resolveoneIn(EPC::ModelElement::toNode).oclAsType(PN::Place);
    }
    else {
        mainNode := object PN::Place {
            id := 'p'.uniqueId();
            name := object PN::Name { text := 'Or' };
        };
        result += mainNode;
    };

    // Input nodes
    if (not inputIsEvent) {
        if (self.inputs->size() = 1) {
            var sourceNode := self.inputs->at(1).resolveoneIn(EPC::ModelElement::toNode);
            result += object PN::Arc {
                id := 'a'.uniqueId();
                source := sourceNode;
                target := mainNode;
            };
        }
        else {
            var n := self.inputs->size();
            var m := 2.pow(n) - 1;
            var transitions := Sequence{1..m}->collect(i | self.xmap toOrJoinTransition(i));
            result += transitions;
            transitions->forEach (transition) {
                result += object PN::Arc {
                    id := 'a'.uniqueId();
                    source := transition;
                    target := mainNode;
                };
            };

            var k := 1;
            self.inputs->forEach (element) {
                var sourceNode := element.resolveoneIn(EPC::ModelElement::toNode);
                var place : PN::Place;
                if (sourceNode.oclIsKindOf(PN::Place)) {
                    place := sourceNode.oclAsType(PN::Place);
                }
                else {
                    place := object PN::Place {
                        id := 'p'.uniqueId();
                    };
                    result += place;
                    result += object PN::Arc {
                        id := 'a'.uniqueId();
                        source := sourceNode;
                        target := place;
                    };
                };
                var i := 1;
                transitions->forEach (transition) {
                    if (i.div(k).mod(2) = 1) {
                        result += object PN::Arc {
                            id := 'a'.uniqueId();
                            source := place;
                            target := transition;
                        };
                    };
                    i := i + 1;
                };
                k := k * 2;
            };
        };
    };

    // Output nodes
    if (inputIsEvent or not outputIsEvent) {
        if (self.outputs->size() = 1) {
            var targetNode := self.outputs->at(1).resolveoneIn(EPC::ModelElement::toNode);
            var transition : PN::Transition;
            if (targetNode.oclIsKindOf(PN::Transition)) {
                transition := targetNode.oclAsType(PN::Transition);
            }
            else {
                transition := object PN::Transition {
                    id := 't'.uniqueId();
                };
                result += transition;
                result += object PN::Arc {
                    id := 'a'.uniqueId();
                    source := transition;
                    target := targetNode;
                };
            };
            result += object PN::Arc {
                id := 'a'.uniqueId();
                source := mainNode;
                target := transition;
            };
        }
        else {
            var n := self.outputs->size();
            var m := 2.pow(n) - 1;
            var transitions := Sequence{1..m}->collect(i | self.xmap toOrSplitTransition(i));
            result += transitions;
            transitions->forEach (transition) {
                result += object PN::Arc {
                    id := 'a'.uniqueId();
                    source := mainNode;
                    target := transition;
                };
            };

            var k := 1;
            self.outputs->forEach (element) {
                var targetNode := element.resolveoneIn(EPC::ModelElement::toNode);
                var place : PN::Place;
                if (targetNode.oclIsKindOf(PN::Place)) {
                    place := targetNode.oclAsType(PN::Place);
                }
                else {
                    place := object PN::Place {
                        id := 'p'.uniqueId();
                    };
                    result += place;
                    result += object PN::Arc {
                        id := 'a'.uniqueId();
                        source := place;
                        target := targetNode;
                    };
                };
                var i := 1;
                transitions->forEach (transition) {
                    if (i.div(k).mod(2) = 1) {
                        result += object PN::Arc {
                            id := 'a'.uniqueId();
                            source := transition;
                            target := place;
                        };
                    };
                    i := i + 1;
                };
                k := k * 2;
            };
        };
    };
}

mapping EPC::Gate::toOrSplitTransition(i : Integer) : PN::Transition
{
    id := 't'.uniqueId();
}

mapping EPC::Gate::toOrJoinTransition(i : Integer) : PN::Transition
{
    id := 't'.uniqueId();
}

// TODO: The simplest variant is implemented
// Or-split should be followed by or-join
// Another gates between or-gates are not allowed
// Look at the following link for the problem description:
// https://researcher.watson.ibm.com/researcher/files/zurich-hvo/bpm-2012.pdf
// Or-joins should be implemented differently
// In some scenarios they can't be implemented in a Petri net at all
mapping EPC::Gate::toOrSync() : List(PN::Object)
when { self.kind = EPC::GateKind::orGate and self.inputs->size() > 1 }
{
    var sourceOrGates := self.inputs->collect(findSourceOrGate())->asOrderedSet();
    assert fatal (sourceOrGates->size() = 1 and sourceOrGates->at(1) <> null) with log('Source or gate not found');

    var splitTransitions := sourceOrGates->at(1).resolveIn(EPC::Gate::toOrSplitTransition);
    var joinTransitions := self.resolveIn(EPC::Gate::toOrJoinTransition);
    assert fatal (splitTransitions->size() > 0) with log('Split or-transitions not found');
    assert fatal (splitTransitions->size() = joinTransitions->size()) with log('There should be the same number of split and join or-transitions');

    Sequence{1..splitTransitions->size()}->forEach (i) {
        var place := object PN::Place {
            id := 'p'.uniqueId();
        };
        result += place;
        result += object PN::Arc {
            id := 'a'.uniqueId();
            source := splitTransitions->at(i);
            target := place;
        };
        result += object PN::Arc {
            id := 'a'.uniqueId();
            source := place;
            target := joinTransitions->at(i);
        };
    };
}

query EPC::ModelElement::findSourceOrGate() : EPC::Gate =
    let connection = self.model.elements[Connection]->any(target = self) in
    if connection <> null then connection.source.findSourceOrGate()
    else self.model.elements[Gate]->any(kind = EPC::GateKind::orGate and outputs->includes(self)) endif;

query Function::performer() : OrganizationUnit =
  let p : Participation = self.model.elements->selectByKind(Participation)->
    any(kind = ParticipationKind::performs and target = self) in
  if p <> null then p.source.oclAsType(OrganizationUnit) else null endif;

query Model::toName() : PN::Name =
    object PN::Name { text := self.name };

query NamedElement::toName() : PN::Name =
    object PN::Name { text := self.name };

//query NamedElement::toName() : PN::Name =
//    object PN::Name { text := self.name.toUpperFirst() };
//
//query Function::toName() : PN::Name =
//    let performer : OrganizationUnit = self.performer() in
//    object PN::Name {
//        text := if performer <> null then performer.name.toUpperFirst() else 'Участник' endif + ' ' + self.name
//    };
//
//query String::toUpperFirst() : String =
//    self.substring(1, 1).toUpperCase() + self.substring(2, self.size());
